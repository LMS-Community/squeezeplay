dnl Process this file with autoconf to produce a configure script

dnl ------------------------------------------------
dnl Initialization and Versioning
dnl ------------------------------------------------

AC_INIT(mdct.c)

AC_CANONICAL_HOST
AC_CANONICAL_TARGET

AM_CONFIG_HEADER([config.h])

AM_INIT_AUTOMAKE(libvorbisidec,1.2.0)

dnl Library versioning

V_LIB_CURRENT=1
V_LIB_REVISION=2
V_LIB_AGE=0
AC_SUBST(V_LIB_CURRENT)
AC_SUBST(V_LIB_REVISION)
AC_SUBST(V_LIB_AGE)

dnl --------------------------------------------------  
dnl Check for programs
dnl --------------------------------------------------  

dnl save $CFLAGS since AC_PROG_CC likes to insert "-g -O2"
dnl if $CFLAGS is blank
cflags_save="$CFLAGS"
AC_PROG_CC
AC_PROG_CPP
CFLAGS="$cflags_save"

AC_PROG_LIBTOOL
AM_PROG_LIBTOOL

AM_PROG_AS

dnl --------------------------------------------------
dnl Set build flags based on environment
dnl --------------------------------------------------

dnl Set some target options

cflags_save="$CFLAGS"
ldflags_save="$LDFLAGS"
if test -z "$GCC"; then
        case $host in 
        arm*)
		cpu_arm=true
                DEBUG="-g -D_ARM_ASSEM_" 
                CFLAGS="-O -D_ARM_ASSEM_"
                PROFILE="-p -g -O -D_ARM_ASSEM" ;;
        *)
                DEBUG="-g -DONLY_C"
                CFLAGS="-O -DONLY_C"
                PROFILE="-g -p -DONLY_C" ;;
        esac
else

        case $host in 
        arm*)
		cpu_arm=true
                DEBUG="-g -Wall -W -D__NO_MATH_INLINES -fsigned-char -D_ARM_ASSEM_"
                CFLAGS="-O2 -Wall -D_ARM_ASSEM_ -fsigned-char"
                PROFILE="-Wall -pg -g -O2 -D_ARM_ASSEM_ -fsigned-char -fno-inline-functions";;

        *)
                DEBUG="-g -Wall -W -D__NO_MATH_INLINES -DONLY_C -fsigned-char"
                CFLAGS="-O2 -Wall -DONLY_C -fsigned-char"
                PROFILE="-Wall -pg -g -O2 -DONLY_C -fsigned-char -fno-inline-functions";;
        esac
fi
CFLAGS="$CFLAGS $cflags_save -D_REENTRANT"
LDFLAGS="$LDFLAGS $ldflags_save"

AM_CONDITIONAL(TREMOR__CPU_ARM, test "x$cpu_arm" = xtrue)

# Test whenever ld supports -version-script
AC_PROG_LD
AC_PROG_LD_GNU
if test "x$lt_cv_prog_gnu_ld" = "xyes"; then
   SHLIB_VERSION_ARG="Wl,--version-script=Version_script"

   dnl Set extra linker options
   case "$target_os" in
  linux* | solaris* )
    SHLIB_VERSION_ARG="-Wl,--version-script=Version_script"
    ;;
  *)
    ;;
   esac
   LDFLAGS="$LDFLAGS $SHLIB_VERSION_ARG"
fi

dnl --------------------------------------------------
dnl Options 
dnl --------------------------------------------------

AC_ARG_ENABLE(
   low-accuracy,
   [  --enable-low-accuracy   enable 32 bit only multiply operations],
   CFLAGS="$CFLAGS -D_LOW_ACCURACY_"
)

AC_ARG_ENABLE(
   64kHz-limit,
   [  --enable-64kHz-limit   limit playback ability to <64kHz files],
   CFLAGS="$CFLAGS -DLIMIT_TO_64kHz"
)

dnl --------------------------------------------------
dnl Check for headers
dnl --------------------------------------------------

AC_CHECK_HEADER(memory.h,CFLAGS="$CFLAGS -DUSE_MEMORY_H",:)

dnl --------------------------------------------------
dnl Check for typedefs, structures, etc
dnl --------------------------------------------------

dnl none

dnl --------------------------------------------------
dnl Check for library functions
dnl --------------------------------------------------

AC_FUNC_ALLOCA
AC_FUNC_MEMCMP

dnl --------------------------------------------------
dnl Do substitutions
dnl --------------------------------------------------

LIBS="$LIBS"

AC_SUBST(LIBS)
AC_SUBST(DEBUG)
AC_SUBST(PROFILE)

AC_OUTPUT(Makefile Version_script)
